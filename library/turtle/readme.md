# Turtle Graphics

Turtle graphics implementation in Forth. It's built on the [`pixels`](../pixels/) library and is meant to run on the [interpreter](../../interpreter/) and will be ported to future VMs.

The Turtle starts at the center of the canvas, heading _North_. You may `go` to another point or `head` in another direction (or use `pose` to do both at once). `start` to clear the canvas and reset the pose. Use `turn` and `move` to draw. There is no pen-up/down, but instead a `jump` which moves without plotting.

Test it with some demos: [`sh ./test.sh`](./test.sh) or play with it interactively: [`sh ./play.sh`](./play.sh)

## Vocabulary

| Word    | Signature | Description                           |
| ------- | --------- | ------------------------------------- |
| `x`     |    -x     | Get x coordinate                      |
| `y`     |    -y     | Get y coordinate                      |
| `theta` |    -t     | Get theta heading                     |
| `plot`  |  xy-      | Plot dot at coordinate                |
| `go`    |  xy-      | Set coordinate (theta)                |
| `head`  |   t-      | Set heading (x/y)                     |
| `pose`  | xyt-      | Set pose (x/y/theta)                  |
| `start` |    -      | Start drawing (init)                  |
| `turn`  |   d-      | Turn relative degrees                 |
| `move`  |   s-      | Move relative steps (and draw)        |
| `jump`  |   s-      | Jump relative steps (without drawing) |

## Walkthrough

Turtle graphics is based on a _turtle_ that leaves a trail as it moves (we don't support raising the pen). The turtle maintains a heading and starts at the center of the canvas, which we consider to be coordinate 0, 0.

```forth
var x var y var theta
var dx var dy
```

 The complete turtle pose can be expressed with three numbers `x`, `y` and `theta`. We will also maintain a delta x/y to avoid recomputing while the heading hasn't changed.

```forth
: go y ! x ! ;
: head dup theta ! deg2rad dup cos dx ! sin dy ! ;
: pose head go ;
```

We can `go` to a particular coordinate (without drawing) and may `head` in a particular direction in degrees. Notice that this is converted to radians and the deltas are updated.

```forth
3.14159265359 const pi
pi 180.0 / const rads
180.0 pi / const degs
: deg2rad rads * ;
: rad2deg degs * ;
```

The `deg2rad` and `rad2deg` conversion words are defined as above. Note that computation that can be done at compile time is placed in constants.

```forth
: plot valid? if point-x point-y set then ;
```

We can `plot` a dot where the turtle is, assuming it's a valid point on the canvas (`valid?`). Note that it's a common naming convention to end the names of predicate words with `?`.

```forth
: point-x x @ width 2 / + 0.5 + floor ;
: point-y y @ height 2 / + 0.5 + floor ;
: valid-x? point-x 0 width 1 - between ;
: valid-y? point-y 0 height 1 - between ;
: valid? valid-x? valid-y? and ;
```

A `valid?` point is one that is within the 160×160 (`width`×`height`) canvas. The `point-x` and `point-y` words essentially shift the origin to the top left corner. The `valid-x?` and `valid-y?` words check that the coordinate is `between` 0 and the corresponding edge.

```forth
: between rot swap over >= -rot <= and ;
```

`between` is defined in the prelude as above; taking a value and a lower and upper bound. It rotates the value to the top, does a `swap over` to essentially bury a copy of the value three elements deep, then bounds checks each pair.

```forth
: start clear 0 0 0 pose ;
: turn theta @ + head ;
: move times dx @ x +! dy @ y +! plot loop ;
: jump dup dx @ * x +! dy @ * y +! ;

```

We can use `turn` and `move` for _relative_ turning and movement. While moving we also `plot` the points traversed. If instead we `jump` then the coordinates are merely updated without drawing.

This is a complete turtle graphics system!

## Let's Play!

Let's have some fun playing with turtle graphics. `start 4 times 30 move 90 turn loop show` produces a square.

```text
⡏⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⡇  ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⡇⠀  ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⡇  ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⡇  ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⡇  ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⡇  ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠧⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠇⠀
```

Similarly we can make a triangle with `start 3 times 30 move 120 turn loop show`.

```test
⠹⡉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⡹⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠘⡄⠀⠀⠀  ⠀⠀⠀⠀⠀⠀⠀⡜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠘⢄⠀ ⠀ ⠀⠀⠀⠀⠀⢀⠜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
 ⠀⠀⠀⠘⡄⠀⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
  ⠀⠀⠀⠈⢆⠀⠀⠀⢠⠋⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
 ⠀⠀⠀⠀⠀⠈⢆⠀⣠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
 ⠀⠀⠀⠀⠀⠀⠀⠳⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

To generalize this pattern:

```forth
: angle 360 swap / ;
: draw -rot times 2dup move turn loop 2drop ;
: polygon dup angle draw ;
```

We can draw _any_ polygon by computing the turn `angle` and iterating turning and moving for each side. A little stack juggling is happening to get the final signature of `polygon` to take a length and the number of sides. For example, our square becomes `30 4 polygon` and our triangle `30 3 polygon`.

```forth
: triangle 3 polygon ;
: square   4 polygon ;
: pentagon 5 polygon ;
: hexagon  6 polygon ;
: circle  36 polygon ;
```

We can give these names. Note that leaving off the length argument to `polygon` in the definitions essentially creates words expecting that _one_ argument (e.g. `30 square`, `30 triangle`). If you've used a functional ML language like Haskell or F#, then you may recognize this as partial application or Currying.

It's interesting too that a polygon at the limit is essentially a circle.

```forth
: shapes start 0 -70 go 50 hexagon 50 pentagon 50 square 50 triangle show ;

shapes
```
All at once!

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⠤⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡾⡏⢢⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠞⡿⣆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⢎⠇⡇⠀⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠎⠀⡇⢏⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠋⡎⠀⡇⠀⠀⠱⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠋⠀⠀⡇⠈⡎⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⡠⠃⡸⠀⠀⡇⠀⠀⠀⠱⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠃⠀⠀⠀⡇⠀⠸⡀⠣⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⡰⠁⢀⠇⠀⠀⡇⠀⠀⠀⠀⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡰⠁⠀⠀⠀⠀⡇⠀⠀⢇⠀⠱⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⡔⠁⠀⡜⠀⠀⠀⡇⠀⠀⠀⠀⠀⠘⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡔⠁⠀⠀⠀⠀⠀⡇⠀⠀⠘⡄⠀⠑⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⢀⡜⠀⠀⣰⠁⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠈⢆⠀⠀⠀⠀⠀⠀⠀⢀⡜⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⢱⡀⠀⠘⣄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⢀⠎⠀⠀⢠⠃⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⢇⠀⠀⠀⠀⠀⢠⠊⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⢣⠀⠀⠈⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⢠⠎⠀⠀⠀⡜⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠈⢦⠀⠀⠀⡠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠘⡄⠀⠀⠈⢦⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⢠⠃⠀⠀⠀⢰⠁⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠀⡰⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⢱⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠐⡅⠀⠀⠀⢀⠇⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⢇⠀⠀⠀⠀⡕⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠘⢄⠀⠀⠸⣀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⢀⡸⠀⠀⢀⠜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠈⢆⠀⠀⠀⠙⠤⣀⠀⠀⠀⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠁⠀⠀⢀⡠⠜⠁⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠘⢆⠀⠀⠀⠀⠈⠒⢤⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠔⠊⠀⠀⠀⠀⢠⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠈⢆⠀⠀⠀⠀⠀⠀⠉⠒⢄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠔⠊⠁⠀⠀⠀⠀⠀⣠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⠀⠉⠢⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡠⠊⠁⠀⠀⠀⠀⠀⠀⠀⡰⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠣⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⠤⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠔⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⡴⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠱⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠑⢤⡀⠀⠀⠀⠀⣠⠔⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠲⢄⠴⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⣄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

We can use the `draw` word directly to get things other than regular polygons.

```forth
: star 5 144 draw ;

30 star
```

```text
⠀⠀⠀⠀⠀⠀⠀⡰⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⢠⠃⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⡜⠀⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠙⠯⡉⠉⠉⡹⠉⠉⠉⠹⡉⠉⠉⡩⠟⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠈⠓⢤⡃⠀ ⠀⠀⠀⣣⠔⠋⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⡜⠈⠒⢄⠔⠊⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⢰⢁⡠⠊⠁⠉⠢⣀⢱⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠟⠁⠀⠀ ⠀⠀⠀⠀⠙⠇⠀⠀⠀⠀⠀⠀⠀
```

The following is a _very_ tricky higher order word that takes other words (named or anonymous) as an argument; a function that takes a function.

```forth
: spin dup angle swap times 2dup turn call loop 2drop ;
```

Given a number of times to spin along with a shape-drawing word as an argument, it uses `call` to invoke the shape repeatedly while turning the turtle.

```forth
: stars start [: 80 star :] 3 spin show ;

stars
```

We give it a size 80 star as an anonymous definition and ask it to `spin` 3 times.

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡦⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠈⠦⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢱⠀⠀⠑⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⠀⠀⠀⠑⣄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⠤⠔⠉⡝⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⡄⠀⠀⠀⠀⠀⠣⡄⠀⠀⠀⠀⠀⢀⡠⠔⠊⠉⠀⠀⢀⡜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠈⠢⣀⡤⠒⠉⠁⠀⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⢀⡠⠔⠒⠉⠈⠦⡀⠀⠀⠀⠀⠀⠀⢠⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⣸⠤⠒⠊⠁⠀⠀⠀⠀⠀⠀⠑⢆⠀⠀⠀⠀⢠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡠⠔⠒⠁⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⣄⠀⡰⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⠤⠔⠋⠁⠀⠀⠀⠀⠈⡆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡷⡅⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡠⠴⠊⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡜⠀⠈⠢⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠉⠑⠒⠒⠤⠤⢄⣀⡀⠀⠀⠀⠀⢇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠊⠀⠀⠀⠀⠉⢆⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠉⠉⠒⠒⢺⠤⢄⣀⣀⠀⠀⠀⠀⠀⣠⠃⠀⠀⠀⠀⠀⠀⠀⠑⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢰⢱⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⢸⠀⠀⠀⠀⠉⠉⠑⠒⡲⠥⠤⢄⣀⡀⠀⠀⠀⠀⠀⠀⠑⣄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡇⠀⣇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⡆⠀⠀⠀⠀⠀⠀⡔⠁⠀ ⠀⠀⠀⠈⠉⠉⠒⠒⠤⠤⢄⣀⣣⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡜⠀⠀⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⡜⠀⠀⠀⠀   ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢰⠁⠀⠀⠀⢱⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⠀⢠⠎⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡸⠀⠀⠀⠀⠀⠀⠸⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⢠⠃⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡔⠃⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠃⠀⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡷⠃⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠋⠀⢀⠇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠘⠯⡉⠉⠉⠉⠉⠉⠉⠉⠉⠉⢉⠏⠉⠉⠉⠉⠉⠉⠉⠉⢏⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⡩⠟⢫⡒⠤⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠎⠀⠀⠀⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠈⠓⠤⡀⠀⠀⠀⠀⠀⠀⠀⠀⡸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠸⡀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠔⠋⠀⠀⠀⠱⡀⠀⠉⠑⠢⢄⡀⠀⠀⠀⠀⠀⠀⠀⢀⠖⠁⠀⠀⠀⠀⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠈⠒⢄⠀⠀⠀⠀⠀⢠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⢀⠔⠊⠀⠀⠀⠀⠀⠀⠀⠱⡄⠀⠀⠀⠀⠈⠑⠒⠤⣀⡀⢀⡔⠁⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠉⠢⣀⠀⠀⡎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⡆⠀⢀⡠⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⡭⠓⠢⢄⣀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⡼⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡸⡜⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⢆⠀⠀⠀⠀⠀⡠⠋⠀⠀⠀⠀⠀⠙⠒⠤⣀⢠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠃⠀⠓⠤⡀⠀⠀⠀⠀⠀⠀⠀⡠⠔⠃⠀⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⢆⠀⠀⡠⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢹⠑⠦⢄⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡎⠀⠀⠀⠀⠉⠒⢄⠀⠀⢀⠔⠊⠁⠀⠀⠀⠈⡆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠖⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡸⠀⠀⠀⠈⠉⠒⠤⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⠀⠀⠀⠀⠀⠀⢀⡩⠪⣁⠀⠀⠀⠀⠀⠀ ⠀⢸⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡔⠁⠳⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠉⠑⠢⢄⡀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠇⠀⠀⠀⠀⢀⡠⠔⠁⠀⠀⠀⠑⠤⣀⠀⠀⠀⠀⠀⢇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡤⠃⠀⠀⠀⠱⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⢀⣀⣀⠤⠤⠔⠚⠓⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⡜⠀⠀⠀⣠⠔⠉⠀⠀⠀⠀⠀⠀⠀⠀⠈⠑⢤⡀⠀⠀⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠊⠀⠀⠀⠀⠀⠀⠙⡄⠀⠀⠀⠀⠀⠀⠀⠀⢀⣸⣀⠤⠤⠔⠒⠋⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⣀⠴⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠲⢄⡀⢸⠀⠀⠀⠀⠀⠀⠀⠀⡠⠎⠁⠀⠀⠀⠀⠀⠀⠀⠀⣘⣄⡠⠤⠔⠒⠒⠉⠉⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⢀⠧⠚⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⠢⢇⠀⠀⠀⠀⠀⢀⠖⠁⠀⣀⠤⠤⠒⠒⠊⠉⠉⠀⠈⢆⠀⠀⠀⠀⠀⠀⠀⡜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠐⠓⠊⠉⠉ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⢣⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⡀⠀⠀⠀⢀⠇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠱⡀⠀⠀⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⡄⠀⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⡄⡎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

Or 15 circles:


```forth
: spiro start [: 4 circle :] 15 spin show ;

spiro
```

This is the power of higher order words. Remember that the phrase `[: 4 circle :]` pushes the address of the anonymous definition. Creating a proper definition such as `: small-circle 4 circle ;` and then using the expression `' small-circle` would do the same thing.

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⣀⠤⠤⠤⣀⣀⣀⣀⣀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⣀⢤⠤⠖⠛⠥⢤⡤⣔⠒⠉⠉⠈⠑⠢⣌⠉⠑⠢⢄⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠤⠊⢁⠔⠉⠀⠀⠀⡔⠊⠀⠀⠉⠒⢤⡠⠔⠒⠋⠉⢏⡉⠉⠙⠲⡤⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⣔⠕⠒⢩⠋⠉⠉⠉⢱⠛⠤⣀⠀⠀⡠⠜⠁⠑⢄⠀⠀⠀⠀⠱⣀⣀⣀⠘⢄⠑⢢⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⣀⢔⠏⠀⠀⢠⠃⠀⠀⠀⢠⠃⠀⠀⠈⢑⢾⡁⠀ ⠀⠀⠈⢢⠤⠒⠉⠉⢣⠀⠀⠉⠉⡗⠦⣙⢄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⢀⡔⠁⣎⡠⠤⠒⡺⠒⠒⠒⡮⢄⠀ ⠀⢀⠜⠀⠘⢄⠀⡰⠊⠁⡇⠀⠀⠀⠀⡇⠀⠀⠀⢸⠀⠀⠙⢧⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⡎⡤⢲⠃⠀⠀⠀⡇⠀⠀⠀⠀⡇⠀⠉⠒⣼⠀⠀ ⠀⢀⢏⠀⠀⠀⢸⣀⡠⠤⠒⡗⠒⠒⠒⠪⡦⣀⠀⠸⡉⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⣸⠊⠀⢸⠀⠀⢀⣀⣱⠤⠤⢄⣀⡇⠀⠀⠀⡇⠙⡄⢠⠎⠘⡄⢀⠤⢺⠀⠀⠀⢀⠇⠀⠀⠀⢰⠁⠀⠉⠲⣇⠈⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⡴⡏⠀⠀⡠⡗⠊⠁⠀⠈⡆⠀⠀⠀⠸⡉⠒⢤⢇⠀⠈⣏⠀⠀⡷⠁⠀⡜⢀⣀⡠⡼⠤⠤⠤⣀⡜⠀⠀⠀⠀⡇⠳⣸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⢰⠁⢣⡰⠉⠀⠑⡄⠀⠀⠀⢈⣆⣀⣀⣀⣱⡀⠀⠹⡦⡀⡟⡄⡎⡇⢀⣴⠓⠉⢀⡜⠀⠀⠀⠀⡜⠉⠉⠒⢄⡸⠀⠀⠸⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⢸⢀⠾⡀⠀⠀⠀⣘⣔⠒⠉⠁⠀⠓⣄⠀⠀⠙⢵⠦⣱⡱⡇⣿⢰⢗⡱⣁⡤⡤⠮⠤⠤⢄⣠⠊⠀⠀⠀⠀⣠⠛⢤⠀⢰⢹⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⢸⠎⠀⠳⡀⡠⠊⠀⠀⠓⢄⠀⠀⣀⠤⠝⠲⢖⣒⠫⠶⣝⣼⣷⣧⣚⣭⣖⣉⣀⢀⣠⠔⠊⠀⠉⠑⠢⣀⠔⠁⠀⠀⠱⡜⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⢸⡄⠀⢀⠗⢅⠀⠀⠀⢀⡠⠋⠛⠤⢄⣀⣀⣤⣒⣛⣭⢝⢿⣿⡷⣒⠲⢖⠒⠉⠉⠑⠤⣀⠀⢀⡠⠔⠉⠣⡀⠀⠀⡰⢱⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⢸⢱⠀⡜⠀⠀⠙⠤⣤⠃⠀⠀⠀⢀⡰⠊⠁⠀⡠⣪⢴⢏⡞⣽⠹⣌⢫⡑⠛⠲⣤⠔⠒⠒⠛⢅⠀⠀⠀⠀⠘⣄⠎⠀⠈⣾⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠸⡀⢹⡅⠀⠀⠀⡜⠀⠉⠒⠒⣤⠧⠤⠔⢒⠎⠉⢀⣧⠊⡇⡇⢇⡇⠉⠺⣀⡀⠀⢣⡀⠀⠀⠀⠣⡀⣀⠤⠊⠸⡀⠀⢠⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⢣⢸⠈⠣⢄⣰⠁⠀⠀⠀⢰⠁⠀⠀⢀⠎⢀⡠⢺⠀⠀⣱⠁⠀⢳⡀⠀⢱⠈⠉⠒⢳⠒⠒⠒⠊⢹⠀⠀⠀⠀⣇⡰⢡⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠈⢾⡀⠀⠀⡏⠒⠢⠤⢤⢇⠤⠤⠤⢼⠒⠉⠀⢸⠀⡔⠉⢆⠀⡇⠈⠲⣈⡆⠀⠀⠀⢇⠀⠀⠀⠀⡇⠀⢀⡤⡞⠀⡜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⢗⢆⡀⡇⠀ ⠀⠀⢸⠀⠀⠀⠀⢸⠀⠀⠀⣸⡊⠀⠀⠈⢶⠁⠀⠀⠀⡏⠑⠤⠤⢼⢄⠤⠤⠤⡗⠊⠁⠀⣇⠞⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠈⢆⠈⢳⠤⣀⡀⢸⠀⠀⠀⠀⣸⡠⠒⠉⠀⢣⠀⠀⡰⠋⠱⢄⠀⢰⠁⠀⠀⠀⢸⠀ ⠀⠀⠀⡇⠀⢀⡜⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠈⠱⣈⢆⠀⠈⠉⢏⠉⠉⠉⠀⢇⠀⠀⠀⠈⣣⡖⠁⠀⠀⠀⢩⠫⠤⣀⣀⢀⠇⠀⠀ ⣀⣸⠤⢊⡱⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠙⠦⣄⡀ ⠘⢆⠀⠀⠀⠘⢤⣀⠤⠚⠀⠉⠦⡀⣠⠃⠀⠀ ⠀⢀⠟⠉⠉⠉⡠⢃⡠⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⠮⡉⠙⠶⡒⠊⠉⠁⠣⡄⠀⠀⠀⠀⡨⠛⠤⢄⣀⡀⡠⠎⢀⣀⣠⠾⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠑⠢⠬⢦⣄⣀⣀⣈⠵⠶⣒⡉⠀⠀⠀⢀⠤⠊⢉⣉⠵⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠑⠒⠢⠤⠤⠬⠛⠛⠛⠓⠉⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

Let's finish with a few more examples.

```forth
: burst start 60 times i 6 * head 0 0 go 80 move loop show ;

burst
```

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠠⡀⠀⠀⠀⡆⠀⠀⠀⡆⠀⠀⠀⡆⠀⠀⠀⡠⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠀⠀⠀⠈⡆⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠀⡎⠀⠀⠀⢀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡀⠀⠀⠀⢣⠀⠀⠀⢸⠀⠀⠀⢸⠀⠀⠀⢱⠀⠀⠀⡇⠀⠀⢰⠁⠀⠀⢸⠀⠀⠀⢸⠀⠀⠀⢠⠃⠀⠀⠀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠀⠀⠀⠈⢆⠀⠀⠀⢣⠀⠀⠀⢣⠀⠀⠈⡆⠀⠀⢸⠀⠀⠀⡇⠀⠀⢸⠀⠀⠀⡎⠀⠀⢠⠃⠀⠀⢠⠃⠀⠀⠀⡜⠁⠀⠀⢀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⡀⠀⠀⠈⢆⠀⠀⠀⢇⠀⠀⠈⡆⠀⠀⢱⠀⠀⠘⡄⠀⠀⡇⠀⠀⡜⠀⠀⢰⠁⠀⠀⡎⠀⠀⢀⠇⠀⠀⢀⡜⠀⠀⠀⣠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠒⡄⠀⠀⠀⠘⣄⠀⠀⠀⢣⠀⠀⠈⢆⠀⠀⢸⠀⠀⠈⡆⠀⠀⡇⠀⠀⡇⠀⠀⡇⠀⠀⡎⠀⠀⢸⠀⠀⢀⠎⠀⠀⢀⠎⠀⠀⢀⡜⠀⠀⠀⠀⡔⠂⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠀⠀⠀⠈⠣⡄⠀⠀⠈⢢⡀⠀⠀⠳⡀⠀⠈⡆⠀⠀⢇⠀⠀⢣⠀⠀⡇⠀⠀⡇⠀⠀⡇⠀⢠⠃⠀⢀⠇⠀⠀⡎⠀⠀⢠⠊⠀⠀⣠⠊⠀⠀⠀⡤⠋⠀⠀⠀⢠⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠦⡀⠀⠀⠈⠢⡀⠀⠀⠑⣄⠀⠀⠱⡀⠀⠘⡄⠀⠘⡄⠀⠸⡀⠀⢸⠀⠀⡇⠀⢸⠀⠀⡸⠀⠀⡜⠀⠀⡜⠀⠀⢠⠃⠀⢀⡔⠁⠀⠀⡠⠊⠀⠀⠀⡠⠎⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠐⠤⣀⠀⠀⠀⠉⠢⡀⠀⠀⠈⢢⡀⠀⠈⢆⠀⠀⠙⡄⠀⠙⡄⠀⢸⠀⠀⡇⠀⢸⠀⠀⡇⠀⢸⠀⠀⡇⠀⢸⠀⠀⡜⠁⠀⡰⠁⠀⢀⠎⠀⠀⣠⠊⠀⠀⠀⡠⠊⠁⠀⠀⢀⡠⠔⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠒⢤⡀⠀⠀⠈⠢⣀⠀⠀⠘⢤⠀⠀⠱⡀⠀⠘⢄⠀⠱⡀⠀⢇⠀⢸⠀⠈⡆⠀⡇⠀⡎⠀⢸⠀⢀⠇⠀⡰⠁⠀⡴⠁⠀⡰⠁⠀⢠⠜⠀⠀⢀⡠⠊⠀⠀⠀⣠⠔⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠈⠓⠤⣀⠀⠀⠀⠈⠲⢄⠀⠀⠈⠒⣄⠀⠀⠑⢄⠀⠈⢆⠀⠈⢆⠀⢱⡀⠘⡄⠈⡆⠀⡇⠀⡇⠀⡇⠀⡎⠀⡜⠀⣰⠁⠀⡜⠀⢀⠎⠀⢀⠔⠁⠀⢀⡔⠊⠀⠀⢀⠴⠊⠀⠀⠀⠀⣀⠤⠊⠀⠀⠀⠀⠀
⠀⠀⠀⢀⠀⠀⠀⠀⠀⠙⠢⢄⡀⠀⠀⠙⠢⢄⠀⠀⠑⢄⡀⠈⠱⣀⠀⠱⡀⠈⢣⠀⢱⠀⢱⠀⢱⠀⢇⠀⡇⢀⠇⢰⠁⢰⠁⢰⠁⢀⠎⠀⡰⠁⢀⡰⠉⠀⣀⠔⠁⠀⢀⠤⠚⠁⠀⠀⢀⡠⠔⠉⠀⠀⠀⠀⢀⠀⠀⠀
⠀⠀⠀⠀⠉⠒⠤⣀⠀⠀⠀⠀⠈⠒⠤⣀⠀⠀⠑⠢⡀⠀⠑⢢⡀⠈⠢⡀⠉⢆⠀⢣⡀⢣⠈⣆⠘⡄⢸⠀⡇⢸⠀⡜⢀⡎⢠⠃⢀⠎⢀⠎⠁⡠⠊⠀⣠⠒⠁⠀⡠⠒⠁⠀⠀⣀⠤⠚⠁⠀⠀⠀⢀⡠⠔⠊⠁⠀⠀⠀
⠀⠀⡀⠀⠀⠀⠀⠀⠉⠑⠢⢄⡀⠀⠀⠀⠑⠢⢄⡀⠈⠑⢄⡀⠈⠢⣀⠘⢢⡈⠱⡀⠱⡈⢦⠘⡄⢇⢸⠀⡇⢸⢀⠇⡜⢠⠎⢠⠃⡰⠉⣠⠚⢀⡠⠊⠀⣀⠔⠉⠀⢀⡠⠔⠋⠀⠀⠀⣀⠤⠒⠉⠁⠀⠀⠀⠀⠀⡀⠀
⠀⠀⠈⠉⠒⠒⠤⣀⡀⠀⠀⠀⠈⠉⠒⠤⣄⠀⠀⠉⠒⠤⡀⠈⠣⢄⡈⠲⣀⠘⢤⠑⢆⠑⡌⢆⢱⠸⡈⡆⡇⡎⡸⢰⢁⠎⡰⢃⠖⢡⠜⢀⡰⠊⣀⠤⠋⠀⣀⠤⠒⠁⠀⢀⡤⠔⠊⠉⠀⠀⠀⠀⣀⡠⠔⠒⠊⠉⠀⠀
⠀⡀⠀⠀⠀⠀⠀⠀⠈⠉⠒⠒⠤⢄⣀⠀⠀⠉⠑⠢⢄⣀⠈⠑⠢⢄⠑⠢⢄⠓⢄⠑⢌⠢⡘⡜⡄⡇⡇⡇⡇⡇⡇⡇⡜⡰⡡⢊⠔⢁⠔⢃⠤⠒⢁⡠⠔⠊⢁⣀⠤⠒⠉⠁⠀⢀⣀⠤⠔⠒⠊⠉⠀⠀⠀⠀⠀⠀⠀⡀
⠀⠈⠉⠉⠒⠒⠢⠤⢄⣀⡀⠀⠀⠀⠀⠉⠒⠒⠤⢄⣀⠈⠑⠒⠤⣀⡉⠒⢬⡑⠢⡑⢎⡱⣑⢌⢞⡼⣸⢣⣧⢻⡸⡜⣜⢕⡱⣉⠖⡡⠒⢉⠤⠒⣉⡠⠔⠒⠉⢀⣀⠤⠔⠒⠊⠁⠀⠀⠀⠀⣀⣀⠤⠤⠒⠒⠊⠉⠉⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠉⠉⠒⠒⠢⠤⠤⣀⣀⡀⠉⠑⠒⠢⢤⣈⠓⠢⢌⣑⠪⣕⢜⡪⡪⡪⣾⣧⣿⣿⣧⣟⡼⡪⡪⣚⠔⡩⢔⣊⠥⠒⢋⣠⠤⠒⠒⠉⠁⣀⣀⡠⠤⠤⠒⠒⠊⠉⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠈⠉⠉⠉⠑⠒⠒⠒⠒⠤⠤⠤⠤⠤⣀⣀⣀⣀⡀⠈⠉⠑⠒⠒⠤⠤⢍⣑⡒⠤⠭⣒⡭⣚⢮⣾⣳⣷⣿⣿⣷⣿⣻⢮⣮⡲⣝⡪⠥⠔⣒⣉⠥⠤⠔⠒⠒⠉⠉⣀⣀⣀⣀⡠⠤⠤⠤⠤⠔⠒⠒⠒⠒⠉⠉⠉⠉
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀  ⠀⠀⠀⠀⠈⠉⠉⠉⠉⠑⠒⠒⠒⠒⠢⠬⠭⠭⠶⣚⣭⣿⣽⣿⣿⣿⣿⣿⣿⣿⣽⣯⣝⡲⠮⠭⠭⠤⠒⠒⠒⠒⠒⠉⠉⠉⠉⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠈⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⢉⣉⣉⣉⣉⡩⠭⠭⠭⣭⣛⠿⣿⢿⣿⣿⣿⣾⣿⣿⣿⢿⡿⢟⣫⡭⠭⠭⠭⣉⣉⣉⣉⣉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉⠉
⠀⠀⠀⠀⢀⣀⣀⣀⣀⠤⠤⠤⠤⠤⠒⠒⠒⠒⠊⠉⠉⠉⢉⣁⣀⠤⠤⠔⢒⣊⠭⠭⣒⢮⡻⡿⣽⣻⣿⣿⣿⣿⣿⣻⡿⣝⡫⢖⡪⠭⢍⣒⠒⠤⠤⢄⣀⣉⠉⠉⠉⠉⠒⠒⠒⠒⠢⠤⠤⠤⠤⢄⣀⣀⣀⣀⠀⠀⠀⠀
⠈⠉⠉⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⣀⣀⡠⠤⠤⠒⠒⠊⠉⢁⣀⡠⠴⠒⣉⡡⠔⣒⠭⢊⢕⡪⡪⣪⢟⡷⣿⣿⡷⣷⡯⡪⡪⣚⢕⠮⣑⠒⠤⣉⡑⠲⠤⣀⣀⠉⠉⠒⠒⠢⠤⠤⣀⣀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠉⠉⠉
⠀⠀⠀⠀⣀⣀⡠⠤⠔⠒⠊⠉⠉⠀⠀⠀⣀⣀⠤⠔⠒⠉⢁⣀⠤⠒⢊⡡⠔⢉⡠⢊⡕⢪⢚⢜⢎⢮⢳⡹⡿⣱⢫⢞⡜⢜⢪⠒⣍⠢⣉⠲⢄⡉⠒⠢⢄⣀⠉⠑⠒⠤⢄⣀⡀⠀⠀⠈⠉⠉⠒⠒⠤⠤⣀⣀⡀⠀⠀⠀
⠀⠊⠉⠉⠀⠀⠀⠀⠀⠀⣀⣀⠤⠔⠒⠉⠀⠀⢀⡠⠔⠚⠁⣀⠤⠚⢁⡠⠜⣁⠔⢁⠜⡡⢣⢋⠎⡎⡎⡇⡇⡏⡎⡎⢎⢆⠣⡙⢄⠑⢄⡙⠤⣈⠑⠤⣀⠀⠙⠒⠤⣀⠀⠀⠈⠑⠒⠤⢄⣀⡀⠀⠀⠀⠀⠀⠈⠉⠉⠂
⠀⠀⠀⠀⣀⣀⠤⠒⠊⠉⠀⠀⠀⠀⣀⠤⠖⠉⠁⢀⡠⠔⠋⠀⡠⠔⠃⣠⠒⢁⠴⢁⡜⣠⠃⡜⢰⢡⠃⡇⡇⡇⢣⢱⠘⡌⢆⠘⣄⠱⢄⠑⢢⡀⠓⠤⡀⠉⠢⢄⡀⠀⠉⠑⠦⢄⡀⠀⠀⠀⠈⠉⠒⠢⢄⣀⡀⠀⠀⠀
⠀⠀⠊⠉⠀⠀⠀⠀⠀⢀⡠⠔⠊⠉⠀⠀⣀⠤⠒⠁⠀⢀⠔⠊⠁⡠⠚⢀⡰⠊⢠⠃⡰⠁⡼⢀⠇⡜⢸⠀⡇⢸⠘⡄⢇⠸⡄⢣⠀⢣⠈⠲⣀⠘⠢⡀⠉⠒⢄⠀⠈⠓⠤⣀⠀⠀⠈⠉⠒⠤⣀⠀⠀⠀⠀⠀⠈⠉⠂⠀
⠀⠀⠀⠀⠀⣀⠤⠒⠉⠁⠀⠀⢀⡠⠔⠊⠁⠀⢀⡠⠊⠁⢀⡰⠊⠀⡠⠊⠀⡜⠁⡔⠁⡸⠀⡞⢀⠇⢸⠀⡇⢸⠀⢇⠘⡆⠸⡀⠳⡀⠙⡄⠈⠢⡀⠈⠲⣀⠀⠉⠢⣀⠀⠀⠑⠢⢄⡀⠀⠀⠀⠉⠑⠢⢄⡀⠀⠀⠀⠀
⠀⠀⠀⠐⠉⠀⠀⠀⠀⢀⠤⠒⠉⠀⠀⢀⡠⠔⠁⠀⢀⠔⠃⠀⢠⠚⠀⢠⠋⠀⡜⠀⢰⠁⢸⠀⢸⠀⡜⠀⡇⠘⡄⢸⠀⢸⠀⢱⠀⠱⡀⠈⢣⠀⠘⢢⠀⠀⠓⢄⠀⠀⠑⠤⣀⠀⠀⠈⠒⠤⣀⠀⠀⠀⠀⠈⠑⠀⠀⠀
⠀⠀⠀⠀⠀⠀⡠⠔⠊⠁⠀⠀⠀⣠⠔⠉⠀⠀⣀⠖⠁⠀⢀⠜⠁⠀⡔⠁⢀⠎⠀⢰⠃⢀⠇⠀⡇⠀⡇⠀⡇⠀⡇⠀⡇⠀⢇⠀⢳⠀⠙⡄⠀⠑⡄⠀⠙⢄⠀⠀⠑⢆⡀⠀⠈⠑⢤⡀⠀⠀⠈⠑⠢⢄⡀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠈⠀⠀⠀⠀⣀⠴⠊⠀⠀⠀⡠⠚⠀⠀⢀⠴⠁⠀⢠⠊⠀⢠⠎⠀⢠⠃⠀⡜⠀⢸⠀⠀⡇⠀⡇⠀⡇⠀⢸⠀⠘⡄⠀⢣⠀⠘⢄⠀⠈⢢⠀⠀⠱⢄⠀⠀⠘⠢⡀⠀⠀⠈⠲⢄⡀⠀⠀⠀⠉⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⢀⠤⠚⠀⠀⠀⠀⡠⠊⠀⠀⠀⡰⠊⠀⠀⡔⠁⠀⢠⠃⠀⢀⠇⠀⢰⠁⠀⡎⠀⢸⠀⠀⡇⠀⢸⠀⠈⡆⠀⢱⠀⠀⢇⠀⠈⢆⠀⠀⠑⡄⠀⠈⠲⡀⠀⠀⠈⠢⡀⠀⠀⠀⠘⠢⢄⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡤⠋⠀⠀⠀⡠⠊⠀⠀⢀⠞⠀⠀⡰⠁⠀⢀⠏⠀⢀⠎⠀⢠⠃⠀⢸⠀⠀⡇⠀⢸⠀⠀⢣⠀⠈⢆⠀⠈⢇⠀⠈⢣⠀⠀⠘⢆⠀⠀⠈⠢⡀⠀⠀⠈⠣⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠰⠉⠀⠀⠀⡠⠎⠀⠀⠀⡰⠃⠀⠀⡰⠁⠀⠀⡎⠀⠀⡜⠀⠀⡸⠀⠀⡎⠀⠀⡇⠀⠈⡆⠀⠸⡀⠀⠘⡄⠀⠈⡆⠀⠀⢣⡀⠀⠀⠳⡀⠀⠀⠈⠦⡀⠀⠀⠈⠱⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⠎⠁⠀⠀⢀⠞⠀⠀⠀⡜⠀⠀⠀⡜⠀⠀⢰⠁⠀⠀⡇⠀⠀⡇⠀⠀⡇⠀⠀⡇⠀⠀⡇⠀⠀⢱⠀⠀⠘⡄⠀⠀⠱⡀⠀⠀⠘⢆⠀⠀⠀⠉⢆⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡰⠊⠀⠀⢀⠞⠀⠀⠀⡜⠀⠀⠀⡎⠀⠀⢸⠀⠀⢀⠇⠀⠀⡇⠀⠀⢇⠀⠀⢸⠀⠀⠈⡆⠀⠀⠘⡄⠀⠀⠑⡄⠀⠀⠈⠲⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠐⠁⠀⠀⢀⠎⠀⠀⠀⡰⠁⠀⠀⡸⠀⠀⠀⡇⠀⠀⢸⠀⠀⠀⡇⠀⠀⢸⠀⠀⠀⡇⠀⠀⠸⡀⠀⠀⠱⡀⠀⠀⠘⡄⠀⠀⠀⠑⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠋⠀⠀⠀⡰⠁⠀⠀⢰⠁⠀⠀⢸⠀⠀⠀⢸⠀⠀⠀⡇⠀⠀⢸⠀⠀⠀⢸⠀⠀⠀⢱⠀⠀⠀⠱⡀⠀⠀⠈⠂⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠐⠁⠀⠀⠀⡎⠀⠀⠀⡎⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠈⡆⠀⠀⠈⡆⠀⠀⠀⠑⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠀⠀⠀⠠⠃⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠀⡇⠀⠀⠀⠣⠀⠀⠀⠈⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

```forth
: squaral start -70 -35 go 20 times 140 move 126 turn loop show ;

squaral
```
(`squaral` is a square spiral; to be pronounced with a Russian accent)

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡸⠑⢄⠀⠀⠀⠀⠀⠀⠀⠀⢀⠔⠹⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠇⠀⠈⠦⡀⠀⠀⠀⠀⠀⡠⠎⠀⠀⢇⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠉⠦⣀⠀⠀⠀⠀⠀⠀⡜⠀⠀⠀⠀⠱⢄⠀⠀⢀⠴⠁⠀⠀⠀⠘⡄⠀⠀⠀⠀⠀⢀⡠⠎⠁⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠓⠤⡀⠀⠀⡸⠀⠀⠀⠀⠀⠀⠈⢦⢠⠎⠀⠀⠀⠀⠀⠀⠸⡀⠀⠀⡠⠔⠃⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠈⠒⢤⠇⠀⠀⠀⠀⠀⠀⠀⡴⠱⡄⠀⠀⠀⠀⠀⠀⠀⢧⠔⠊⠀⠀⠀⠀⠀ ⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢲⠤⢄⣀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⡜⠉⠲⣀⠀⠀⠀⢀⠎⠀⠀⠈⢆⠀⠀⠀⢀⡰⠊⠙⡄⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⢀⣀⠤⢴⠂⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⡆⠀⠀⠉⠑⠒⠢⠤⣀⡇⠀⠀⠀⠀⠀⠀⢰⠁⠀⠀⠀⠙⠢⣰⠁⠀⠀⠀⠀⠀⢱⡠⠚⠁⠀⠀⠀⢱⠀⠀⠀⠀⠀⠀⠀⣇⡠⠤⠒⠒⠉⠁⠀⠀⡎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠸⡀⠀⠀⠀⠀⠀⠀⠀⡏⠑⠒⠢⠤⣀⣀⠇⠀⠀⠀⠀⢀⠜⠈⠑⠤⡀⠀⡠⠔⠉⠘⢄⠀⠀⠀⠀⠀⢇⣀⡠⠤⠒⠒⠉⡇⠀⠀⠀⠀⠀⠀⠀⡸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⡜⠉⠒⠢⠤⣠⡃⠀⠀⠀⣀⠜⠚⢄⡀⠀⠀⠀⣣⡠⠤⠒⠊⠙⡄⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⢠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⡆⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⢰⠁⠀⠀⢀⠜⠀⠈⢉⠶⠚⠤⢄⣀⠤⠜⠲⢎⠉⠀⠘⢄⠀⠀⠀⢱⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⡎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⢲⡒⠒⠒⠒⠒⠒⠒⠒⠺⡒⠒⠒⠒⠒⡗⠒⠒⠒⢒⠗⠒⠒⢲⠛⠒⣲⣒⠷⠶⠒⠚⠓⠒⠛⠒⠲⠶⢗⣲⡒⢳⠒⠒⢗⠒⠒⠒⡗⠒⠒⠒⠒⠒⡺⠒⠒⠒⠒⠒⠒⠒⠒⠒⣲⠂⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠑⣄⠀⠀⠀⠀⠀⠀⠀⠀⢇⠀⠀⠀⠀⠀⡇⠀⠀⠀⡸⢀⣀⡼⣵⠒⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠑⢲⡽⣄⣀⠸⡀⠀⠀⠀⡇⠀⠀⠀⠀⢀⠇⠀⠀⠀⠀⠀⠀⠀⢀⡔⠁⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠈⢆⠀⠀⠀⠀⠀⠀⠀⠘⡄⠀⠀⠀⣀⡧⠤⠔⢲⠋⣡⠿⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠹⢧⡉⢳⠒⠤⠤⣇⡀⠀⠀⠀⡜⠀⠀⠀⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠱⡀⠀⠀⠀⠀⣀⡠⠽⡔⠚⠉⠀⡇⠀⢀⠧⡞⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⡦⢇⠀⠀⡇⠈⠙⠒⡼⠥⣀⡀⠀⠀⠀⠀⡰⠁⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⣀⡨⢦⠒⠊⠉⠀⠀⠀⢇⠀⠀⠀⡧⠒⣹⠚⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⢺⡑⠢⡇⠀⠀⢀⠇⠀⠀⠈⠉⠒⢢⠮⣀⡀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⢀⣀⡠⠤⠒⠊⠉⠀⠀⠀⠱⡀⠀⠀⠀⠀⠘⣄⠖⠉⡇⣴⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢳⡄⡏⠑⢆⡜⠀⠀⠀⠀⠀⡰⠁⠀⠀⠈⠉⠒⠢⠤⣀⣀⠀⠀
⠙⠥⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⢆⠀⣀⠔⠋⢱⠀⢀⡟⡎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⡞⣇⠀⢰⠉⠓⢄⡀⢀⠎⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠝⠁
⠀⠀⠈⠑⢄⡀⠀⠀⠀⠀⠀⠀⢀⠬⠺⡁⠀⠀⠀⣷⠃⡿⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠸⡇⢳⡇⠀⠀⠀⡹⠪⢄⠀⠀⠀⠀⠀⠀⠀⣀⠔⠉⠀⠀⠀
⠀⠀⠀⠀⠀⠈⠲⢄⠀⢀⡠⠊⠁⠀⠀⠑⢆⢀⠜⠘⣤⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣧⡜⠘⢄⢀⠖⠁⠀⠀⠉⠢⣀⠀⢀⠴⠊⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⡠⠝⠣⢄⠀⠀⠀⠀⠀⡨⠣⡀ ⠀⣿⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣿⡇⠀⡠⠫⡀⠀⠀⠀⠀⢀⠤⠛⠥⡀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⡠⠔⠊⠀⠀⠀⠈⠑⢢⡀⢀⠔⠁⠀⠑⢼⠀⣇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡇⢸⠔⠁⠀⠑⢄⠀⣠⠒⠉⠀⠀⠀⠈⠒⠤⡀⠀⠀⠀⠀
⠀⢀⡔⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⡨⠛⢄⡀⠀⢀⠏⠢⡿⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡸⡧⠊⢇⠀⠀⣀⠜⠫⡀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠒⣄⠀⠀
⠚⠥⢄⣀⡀⠀⠀⠀⠀⠀⠀⢀⠔⠁⠀⠀⠈⠣⣎⡀⠀⡷⣧⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⡷⡇⠀⣈⡦⠋⠀⠀⠀⠑⢄⠀⠀⠀⠀⠀⠀⠀⣀⣀⠤⠝⠂
⠀⠀⠀⠀⠈⠉⠒⠢⠤⣀⣀⠎⠀⠀⠀⠀⠀⡸⠀⠑⠢⡇⠈⢧⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠏⠀⡧⠒⠁⠸⡀⠀⠀⠀⠀⠈⢆⣀⡠⠤⠒⠊⠉⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⢀⡰⠉⠉⠒⠢⠤⣀⣀⠇⠀⠀⠀⡏⠑⠺⡱⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡴⡹⠒⠉⡇⠀⠀⠀⢇⣀⡠⠤⠒⠊⠉⠱⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⣀⠎⠀⠀⠀⠀⠀⠀⠀⡜⠉⠑⠢⠤⣇⣀⠀⢫⠙⢤⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣠⠜⢩⠃⢀⣀⡧⠤⠒⠉⠙⡄⠀⠀⠀⠀⠀⠀⠈⢆⡀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⡰⠁⠀⠀⠀⠀⠀⠀⠀⣰⠁⠀⠀⠀⠀⡇⠀⠉⠙⡖⠤⢯⣢⢄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⢤⣪⠧⠔⡞⠉⠁⠀⡇⠀⠀⠀⠀⢱⡀⠀⠀⠀⠀⠀⠀⠀⠱⡀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⢀⠇⠀⠀⠀⠀⠀⡇⠀⠀⠀⢸⡀⠀⠘⢍⠙⠲⣤⢄⣀⠀⠀⠀⠀⠀⠀⠀⠀⢀⣀⢤⡴⠚⢉⠝⠀⠀⣸⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⢇⠀⠀⠀⠀⠀⠀⠀⠀⠈⢆⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠉⠉⠉⠉⠉⠉⠉⠉⠉⡝⠉⠉⠉⠉⠉⡏⠉⠉⠉⠉⢫⠉⠉⢫⡉⠉⠙⠯⡉⠉⢛⣯⢭⣟⠋⠉⡩⠟⠉⠉⣩⠋⠉⢩⠋⠉⠉⠉⠉⡏⠉⠉⠉⠉⠙⡍⠉⠉⠉⠉⠉⠉⠉⠉⠉⠁⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢰⠁⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠈⡆⠀⠀⠀⣘⣤⠤⠒⠊⠛⠥⡀⠀⡠⠝⠋⠒⠢⢤⣜⡀⠀⠀⠀⡎⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⢱⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⣀⡠⠼⡒⠊⠉⠀⠀⢣⠀⠀⠀⢀⡝⠚⣅⠀⠀⠀⢠⠃⠀⠈⠉⠒⡺⠤⣀⡀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡸⠀⠀⠀⠀⠀⠀⣀⡠⡗⠒⠉⠉⠀⠀⠀⢇⠀⠀⠀⠀⠀⢙⡤⠊⠁⠀⠀⠀⠉⠢⣜⠁⠀⠀⠀⠀⢀⠇⠀⠀⠈⠉⠑⠒⡧⣀⡀⠀⠀⠀⠀⠀⠸⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢰⢁⣀⠤⠔⠒⠊⠉⠀⠀⡇⠀⠀⠀⠀⠀⠀⠘⡄⠀⢀⡠⠜⠁⠈⢢⠀⠀⠀⠀⢠⠊⠀⠙⠤⣀⠀⡜⠀⠀⠀⠀⠀⠀⠀⡇⠀⠈⠉⠒⠒⠤⢄⣀⢱⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠁⠀⠀⠀⠀⠀⠀ ⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⣸⡔⠊⠀⠀⠀⠀⠀⠑⡄⠀⡔⠁⠀⠀⠀⠀⠈⠒⣼⡀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⡇⠀⠀⠀⠀⢀⠔⠊⠀⢇⠀⠀⠀⠀⠀⠀⠀⢨⢪⠀⠀⠀⠀⠀ ⠀⠀⢀⠇⠈⠒⢄⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⡇⠀⢀⡠⠊⠁⠀⠀⠀⠘⡄⠀⠀⠀⠀⠀⡰⠃⠀⠳⡀⠀⠀⠀⠀⠀⡜⠀⠀⠀⠀⠉⠢⣀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⡧⠒⠁⠀⠀⠀⠀⠀⠀⠀⢱⠀⠀⠀⢀⠞⠀⠀⠀⠀⠘⢆⠀⠀⠀⢰⠁⠀⠀⠀⠀⠀⠀⠀⠑⠢⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢇⠀⡰⠃⠀⠀⠀⠀⠀⠀⠀⠳⡀⢀⠇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⠞⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⠞⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

```forth
: rose start 0 54 times 2 + dup move 84 turn loop show ;

rose
```

An interesting trick is used here, by pushing a `0` before entering the loop and doing `2 + dup` within. This increased the `move` distance on each iteration; spiraling out from the center.

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡠⠎⠁⠓⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠔⠁⠀⠀⠀⠀⠈⠣⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠔⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠦⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠴⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⢆⠀⠀⠀⠀⠀⣀⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡠⠚⠁⠀⠀⠀⣠⡀⠀⠀⠀⠀⠀⠀⠀⠀⢀⣀⡠⠤⠤⠓⢖⠊⠉⠉⠀⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠓⠒⠒⠤⣤⢔⣉⡀⠀⠀⠀⢠⠚⠀⣈⣲⣤⠤⠤⠒⠒⠉⠉⠁⠀⠀⠀⠀⠀⠀⠓⡄⠀⠀⠀⠹⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡜⠀⣀⠴⠉⢀⣀⣀⠬⠭⢽⠞⠓⠫⠭⢄⣀⣀⠙⠢⣀⠀⠀⠀⠀⣀⠀⠀⠀⠀⠀⠀⠈⠣⡀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⣀⣠⣴⠥⠚⠒⠉⠉⠁⠀⠀⢀⠔⠁⠀⠀⠀⠀⠀⠀⠀⠉⠉⢚⣓⠦⡶⠭⣀⣣⡀⠀⠀⠀⠀⠀⠀⠈⠦⡀⠘⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡠⠤⠔⠒⠒⠉⠉⠁⡤⢒⠇⠀⠀⠀⠀⠀⠀⢀⡔⠁⠀⠀⠀⠀⠀⠀⠀⣀⡠⠖⠊⠁⠀⠀⠈⠑⢄⡀⠹⡉⠉⠒⠒⠢⠤⢄⣀⣑⢆⢱⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⠀⠀⠀⠀⣀⠖⠉⠀⡼⠀⠀⠀⠀⠀⠀⡤⠃⠀⠀⠀⠀⡀⣠⠔⠒⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠢⢝⡄⠀⠀⠀⠀⠀⠀⠀⠉⠙⣗⠒⠢⠤⠤⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⡆⠀⣀⠔⠉⠀⠀⠀⢰⠁⠀⠀⠀⣀⣠⣊⣀⣀⣀⣤⣔⣺⣉⣉⣒⣤⣄⣀⣀⣀⣀⣀⣀⣀⣀⣀⣀⣙⣦⣄⣀⡀⠀⠀⠀⠀⠀⠘⡧⡄⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡧⠊⠀⠀⠀⠀⠀⢀⠇⠀⠀⠀⣀⣏⡡⠤⠒⠉⠁⠀⡜⠁⠀⠀⠀⠀⠀⢉⡱⠾⢍⣆⡀⠀⠀⠀⠀⠀⠀⠀⠀⠈⢎⠑⣧⡀⠀⠀⠀⠀⠀⢱⠈⠣⣠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⢀⡠⠒⠁⢣⠀⠀⠀⠀⠀⠀⡸⠀⣀⣠⠔⢻⠁⠀⠀⠀⠀⢀⠜⠀⠀⠀⠀⠀⣠⠔⠉⠀⠀⠀⠀⠙⢖⠤⣀⡀⠀⠀⠀⠀⠀⠈⢢⢸⠈⠑⢄⡀⠀⠀⠀⢇⠀⢸⠦⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⡠⠖⠁⠀⠀⠀⢸⠀⠀⠀⢀⣀⢤⠓⢋⡔⠃⠀⢸⠀⠀⠀⠀⢀⠎⠀⠀⠀⢀⠔⠊⠀⠀⠀⠀⠀⠀⣀⣀⡠⠷⣄⠈⠑⠢⢄⣀⠀⠀⠀⢻⡀⠀⠀⠈⠢⢄⡀⠘⡄⢸⠀⠑⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⣀⠔⠋⠀⠀⠀⠀⠀⠀⢸⠀⠀⠀⠈⣆⠎⡤⠃⠀⠀⠀⡜⠀⠀⠀⢠⠊⠀⢀⠤⢊⣁⣀⠤⠤⠔⠒⠊⠉⠉⠀⠀⠀⠀⠈⡧⡀⠀⠀⠀⠉⠒⠦⣘⡵⡀⠀⠀⠀⠀⠑⠢⣧⡇⠀⠀⠀⠑⣄⠀⠀⠀⠀⠀⠀⠀⠀
⠘⡅⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⡸⡊⠀⠀⠀⠀⠀⡇⠀⠀⣠⠃⣰⠒⠉⠉⠁⢀⠇⠉⠉⠑⢒⡢⠤⠤⣀⣀⡀⠀⠀⠸⡈⠦⡀⠀⠀⠀⠀⠀⡏⢱⡆⠀⠀⠀⠀⠀⠀⣗⠦⡀⠀⠀⠀⠣⡄⠀⠀⠀⠀⠀⠀
⠀⠘⢆⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⣀⢮⠃⢣⠀⠀⠀⠀⢀⠇⠀⣰⠕⠉⠀⡇⠀⠀⠀⡜⠀⠀⢀⡰⠉⠈⠢⢄⠀⠀⠈⠉⠉⠒⣇⠀⠑⢆⠀⠀⠀⠀⢇⡜⠘⡄⠀⠀⠀⠀⢰⠙⡄⠈⡳⠀⠀⠀⠈⠢⡀⠀⠀⠀⠀
⠀⠀⠈⢆⠀⠀⠀⠀⠀⠀⠀⢣⠔⠁⡎⠀⠘⡄⠀⠀⠀⢸⠰⡝⠁⠀⠀⠀⡇⠀⠀⢰⠁⠀⡠⠊⠀⠀⡠⠤⡄⠙⠢⣀⠀⠀⢰⠙⡄⠀⠀⠑⣄⠀⠀⢸⠁⠀⠈⢆⠀⠀⠀⢸⠀⢣⡰⠁⠀⠀⠀⠀⠀⠉⢆⡀⠀⠀
⠀⠀⠀⠈⢣⠀⠀⠀⠀⠀⡔⢻⠀⡸⠀⠀⠀⠱⡀⠀⠀⢸⡜⠱⡀⠀⠀⠀⢣⠀⢀⠇⢠⠚⠀⡔⠊⠉⠀⠀⠘⡄⠀⠈⠑⢢⢸⠀⢱⡀⠀⠀⠀⠓⣤⢿⠀⠀⠀⠈⢆⠀⠀⡼⠀⡜⡆⠀⠀⠀⠀⠀⠀⠀⠀⠑⢆⠀
⠀⠀⠀⠀⠀⠣⡀⠀⠀⠙⣄⠘⣤⠃⠀⠀⠀⠀⢣⠀⢀⡏⠀⠀⠑⡄⠀⠀⢸⠀⡜⠈⢣⡀⠀⠘⡄⠀⠈⡇⠀⠈⢆⠀⢠⠋⡸⠀⠀⢇⠀⠀⠀⠀⡜⡫⡇⠀⠀⠀⠀⢣⠀⣇⠞⠀⠸⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⣑
⠀⠀⠀⠀⠀⠀⠱⡀⠀⠀⠀⠓⡏⠀⠀⠀⠀⠀⠘⣤⠎⡇⠀⠀⠀⠘⣄⠀⠸⣰⠁⠀⠀⠘⢢⠀⠑⠒⠉⠁⣀⠤⠊⡠⠃⠀⡇⠀⠀⠘⡄⠀⠀⣰⠏⠀⡇⠀⠀⠀⠀⠀⢳⡏⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⢀⠔⠁
⠀⠀⠀⠀⠀⠀⠀⠑⡄⠀⠀⡸⡏⠣⡀⠀⠀⠀⠀⢱⢢⡃⠀⠀⠀⠀⠈⢆⠀⡏⠒⠤⢄⡀⠀⠙⢄⠀⡤⠒⠁⠀⡰⠁⠀⠀⡇⠀⠀⠀⢱⢀⢖⠇⠀⠀⢣⠀⠀⠀⠀⢠⢻⠱⡀⠀⠀⠈⡆⠀⠀⠀⠀⠀⡔⠃⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠘⣄⢀⠇⢸⠀⠈⠦⡀⠀⠀⠀⢿⠈⠑⢤⡀⠀⠀⠈⢦⡇⠀⠀⠀⠈⠙⠢⠤⣉⠀⠀⠀⡔⠁⠀⠀⢸⠀⠀⠀⢀⡴⠇⡜⠀⠀⠀⢸⠀⠀⠀⡠⠃⢸⢠⠝⠀⠀⠀⠸⡀⠀⠀⡠⠋⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⣞⠀⢸⠀⠀⠀⠑⢆⠀⠀⡸⡄⠀⠀⠈⠱⢄⡀⠀⢳⣀⣀⣀⣀⣀⣀⣀⣉⣑⣜⣀⣀⣀⣀⣸⠤⠒⡮⠃⠀⢰⠃⠀⠀⠀⠸⡀⠀⡰⢁⡰⡎⠁⠀⠀⠀⠀⠀⢇⡠⠎⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⡸⠉⢦⠈⡆⠀⠀⠀⠀⠑⣄⡇⢱⠀⠀⠀⠀⠀⠘⠢⢄⠱⡀⠀⠀⠀⠀⠀⠀⠀⠀⣀⡠⠔⠒⠁⠀⡠⠊⠀⠀⢀⠇⠀⠀⠀⠀⠀⣇⡴⠛⠁⠀⡇⠀⠀⠀⠀⠀⣀⠞⡇⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⣇⡀⠀⢣⡇⠀⠀⠀⠀⠀⠀⢧⣄⣇⠀⠀⠀⠀⠀⠀⠈⠑⠳⡄⠀⠀⠀⣀⠤⠔⠋⠀⠀⠀⠀⡠⠎⠀⠀⠀⠀⡜⠀⠀⠀⠀⡠⢔⡟⠀⠀⠀⢠⠃⠀⠀⠀⢀⠔⠁⠀⠸⡀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠑⠒⢷⣀⠀⠀⠀⠀⠀⠀⠈⠺⡉⠉⠓⠒⠢⠤⢄⣀⣀⠘⠵⢎⡉⠀⠀⠀⠀⠀⠀⢀⠖⠁⠀⠀⠀⠀⢠⠃⠀⣀⠔⠊⣀⣎⡱⠀⠀⢸⠀⠀⠀⡔⠃⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠱⡉⠒⠤⢄⡀⠀⠀⠀⢫⠦⡀⠀⠀⠀⠀⠀⠀⠉⠉⠑⠚⠓⢤⡤⢄⣀⡔⠁⠀⠀⢀⣀⡠⠤⣮⠶⠚⠊⠉⢩⠎⠀⠀⠀⠀⢸⠀⡠⠋⠀⠀⠀⣀⠤⠒⠉⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⠘⡄⠀⠀⠈⠑⠢⠤⣀⢇⠑⢆⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⣀⡩⠦⠗⠚⠛⠉⠛⠓⢢⡤⠞⠁⠀⠀⠀⢠⠃⠀⠀⠀⠀⠀⡧⠎⢀⡠⠔⠊⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠘⢆⠀⠀⠀⠀⠀⠀⠙⡖⢤⣕⣄⠤⠤⠔⠒⠒⠉⠉⠀⠀⠀⠀⠀⠀⠀⠀⣠⠒⠉⠀⠀⠀⠀⠀⡰⠁⠀⠀⠀⠀⠀⣀⠶⡗⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠈⢆⠀⠀⠀⠀⠀⠀⠉⠁⠀⠈⠫⢦⢄⣀⠀⠀⠀⠀⠀⠀⠀⠀⣀⠴⠉⠀⠀⠀⠀⠀⠀⠀⡴⠁⠀⣀⡠⠔⢒⡝⠁⢰⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⠈⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠲⡀⠉⠒⠢⣀⡀⣀⠤⠚⠀⠀⠀⠀⠀⠀⠀⠀⢀⣜⠤⠒⠊⠀⠀⡤⠃⠀⠀⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠈⠉⠉⠉⠉⠉⠫⡉⠉⠉⠉⠉⠉⠉⠉⠉⢫⡉⠉⡭⠛⠙⠛⢭⣉⠉⠉⠉⢉⡩⠭⢛⠟⠉⠉⠉⠉⡩⠋⠉⠉⠉⠉⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠱⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⠉⠀⠀⠀⠀⠀⣀⠭⠖⠭⢅⡀⢀⠎⠀⠀⠀⡠⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡠⠔⠊⠉⠀⠀⠀⠀⠀⠉⠁⠀⠀⢀⠖⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⣄⠀⠀⠀⠀⠀⠀⠀⣀⠤⠒⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡔⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⢆⠀⢀⡠⠔⠒⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡤⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠊⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠎⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

```forth
: arc times 2dup turn move loop 2drop ;
: petal 2 times 4 6 16 arc 1 -6 16 arc 180 turn loop ;
: flower start ' petal 15 spin show ;

flower
```

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠐⣄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢳⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢦⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠢⠤⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠈⢢⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⡆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢫⡑⠢⡀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠜⠀⠀⢣⡀⠀⠀⠀⠀⠀⠀⠀⠀⢸⢸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠀⠈⠑⢄⡀⠀⠀⠀⠀⡤⠃⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⢀⡠⠜⠁⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠑⢄⠀⢠⠚⠀⠀⠀⠀⠀⠀⠈⡆⠀⣀⠤⠒⠉⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⢰⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⠀⠀⠀⠀⠀⠈⢢⡇⠀⠀⠀⠀⠀⠀⠀⠀⣱⠊⠁⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⣸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠊⠉⠙⠛⢍⠉⠒⠒⠢⠤⣀⠀⠀⡇⠀⠀⠀⠀⠀⠀⢀⡎⢱⠀⠀⠀⠀⠀⠀⡠⠋⢸⠀⠀⠀⠀⠀⠀⠀⠀⢀⠇⠀⠀⠀⠀⠀⠀⢀⡠⠎⡝⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠳⡀⠀⠀⠀⠀⠉⠒⠤⡇⠀⠀⠀⠀⠀⠀⢸⠀⠀⢣⠀⠀⠀⠀⡜⠀⠀⢸⠀⠀⠀⠀⠀⠀⢀⡤⡜⠒⠊⠉⠉⠉⠉⠉⠁⠀⢀⠇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⡏⠲⣀⠀⠀⠀⠀⡇⠀⠀⠈⡆⠀⢀⠎⠀⠀⠀⡜⠀⠀⠀⣠⠒⠉⠁⡰⠁⠀⠀⠀⠀⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⢇⠀⠀⠓⡄⠀⠀⡇⠀⠀⠀⢣⠀⡎⠀⠀⠀⢠⠃⢀⠴⠉⠀⠀⠀⣠⠃⠀⠀⠀⠀⠀⠀⠀⠀⢀⠎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⡄⠀⠀⠀⠀⠀⠀⠸⡀⠀⠀⠘⢆⠀⡇⠀⠀⠀⢸⢸⠀⠀⠀⢀⠎⡔⠁⠀⠀⠀⢀⡜⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠋⠀⠀⠀⠀   ⠀⠀⠀⠀⠀⠀⢠⠂⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⣀⣀⠤⠤⠒⠒⠒⠒⠒⠳⡤⢄⣀⡀⠀⠀⠀⢣⠀⠀⠀⠈⢆⢱⠀⠀⠀⡸⡎⠀⠀⢀⢎⠎⠀⠀⠀⠀⡤⢃⡠⠤⠒⠒⠉⠉⠉⠉⡩⠝⠉⠉⠑⠒⠤⢄⡀⠀⠀⠀⢀⣔⠇⠀⠀⠀⠀⠀⠀⠀
⢀⡤⠔⠛⠒⠢⢄⠀⠀⠀⠀⠀⠀⠀⠀⠱⡀⠀⠈⠒⠤⣀⠈⢣⠀⠀⠀⠈⢞⡄⠀⠀⡇⡇⠀⡠⢪⠎⠀⠀⣀⣖⠭⠚⠁⠀⠀⠀⠀⠀⢀⡠⠒⠁⠀⠀⠀⠀⠀⠀⠀⠈⠉⠉⠉⡱⠁⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠙⡄⠀ ⠀⠀⠀⠀⠀⠀⠘⢆⠀⠀⠀⠀⠑⢢⡣⡄⠀⠀⢸⠱⡀⢰⠁⣇⠎⠁⣇⠤⠒⣩⠒⠁⠀⠀⠀⠀⣀⡠⠔⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⠎⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠘⢄ ⠀⠀⠀⠀⠀⠀⠀⠀⠓⣄⠀⠀⠀⠀⠈⠮⡢⡀⠀⡇⠳⡇⠀⣯⢀⢼⢀⢤⠜⠒⠒⢒⣒⠶⠭⠛⠒⠒⠒⠒⠒⠒⠢⠤⢄⣀⠀⠀⠀⠀⠀⡤⠒⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠲⡀⠀⠀⣀⣀⣀⣀⡀⠀⠑⠦⣀⠀⠀⠀⠱⡌⠲⣣⠀⢻⢄⡟⡜⣷⣩⠧⡴⠖⠚⠣⢄⣀⠀⠀⠀⠀  ⠀⠀⠀⠀⠀⠀⠀⠀⢙⣒⠤⡒⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⢀⣀⠴⠒⠉⠫⢍⡀⠀⠀⠀⠈⠉⠉⠑⠒⠭⣢⡤⣀⣘⢄⠀⠫⣾⠶⣛⣽⣷⣕⣚⣮⠵⠶⢦⡤⠤⠭⠵⢖⣒⠢⠤⠤⠤⠒⠒⠒⠉⠁⠀⠀⠈⠒⢄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⢀⠴⠊⠁⠀⠀⠀⠀⠀⠀⠉⠒⠤⣀⠀⠀⠀⠀⠀⠀⠀⠉⠒⢤⠉⠙⠛⠭⣭⢽⣿⢿⡿⣓⢶⠫⠦⣄⡈⠢⡀⠀⠀⠀⠉⠑⠢⢄⡀⠀⠀⠀⠀⠀⠀⠀  ⠀⠀⠑⢄⠀⠀⠀⠀⠀⠀⡠⠀⠀⠀⠀
⠀⠀⢠⢜⣁⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠉⠒⠒⠢⠤⠤⠤⠤⠤⠤⠛⠒⠚⠛⡲⡫⡾⣼⢹⡜⡝⠽⡀⠘⡍⠒⠭⡲⣀⠀⠀⠀⠀⠀⠈⠒⣄⠀⠀⠀⠀⠀⠀ ⠀⠀⠈⠑⠒⠤⢤⡖⠊⠀⠀⠀⠀⠀
⢀⠔⠉⠁⠀⠈⠉⠒⣄⠀⠀⠀⠀⠀⠀⠀⢀⡠⠤⠒⠒⠉⠉⠉⠉⠉⠉⠉⠑⢒⡖⠚⠉⣡⠗⢹⡎⠘⡇⠀⡗⢆⢣⠀⠀⠈⠑⢍⠲⠤⣀⠀⠀⠀⠀⠑⢄⠀⠀⠀⠀⠀⠀⢀⡤⠔⠊⠁⠀⠀⠀⠀⠀⠀⠀
⠊⠀⠀⠀⠀⠀⠀⠀⠀⠉⠒⠤⣀⣠⠔⠊⠁⠀⠀⠀⠀⠀⠀⠀⠀⣀⡠⢔⡪⠥⠒⠒⡺⠁⡠⢼⠀⡸⢸⠀⡇⠀⠱⡳⡀⠀⠀⠀⠓⡄⠀⠉⠉⠒⠒⠒⠚⠳⡒⠒⠒⠊⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡠⠊⠉⠉⠒⠒⠒⠒⠒⠒⠒⠊⡩⠖⠉⠁⠀⠀⢀⢜⡱⠊⠀⡜⡰⠁⠀⡇⡇⠀⠀⠱⡱⡄⠀⠀⠀⠘⢢⠀⠀⠀⠀⠀⠀⠀⠑⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⣠⠊⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⢀⠔⠉⠀⠀⠀⠀⢀⠴⡝⠃⠀⠀⢠⢻⠀⠀⠀⡇⡇⠀⠀⠀⢱⠈⠢⡀⠀⠀⠀⢇⠀⠀⠀⠀⠀⠀⠀⠸⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⡰⠁⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⢀⠴⠁⠀⠀⠀⢀⡠⠚⢁⠞⠀⠀⠀⢀⠏⡇⠀⠀⠀⡇⢱⠀⠀⠀⠀⡇⠀⠈⠒⢄⠀⠈⢇⠀⠀⠀⠀⠀⠀⠀⢣⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⡜⢁⣀⣀⣀⡀⠀⠀⠀⠀⠀⡠⠊⣀⣀⠤⠔⠊⠁⠀⢀⠎⠀⠀⠀⢀⠎⢸⠀⠀⠀⠀⡇⠈⢆⠀⠀⠀⢣⠀⠀⠀⠀⠉⠒⠼⣄⡀⠀⠀⠀⠀⠀⠀⠑⢄⡀⠀⠀⣀⡀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⣸⠔⠉⠀⠀⠀⠈⠉⠉⠉⠉⢹⠉⠉⠀⠀⠀⠀⠀⠀⢀⠎⠀⠀⢀⡰⠉⠀⢸⠀⠀⠀⢰⠁⠀⠈⢆⠀⠀⢸⠀⠀⠀⠀⠀⠀⠀⡇⠈⠉⠒⠒⠒⠒⠒⠒⠒⠚⠋⠉⠁⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⢀⡇⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⢀⠇⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⠀⡠⠊⠀⠀⠀⢸⠀⠀⠀⡎⠀⠀⠀⠈⢦⠀⡸⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠈⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⡞⠀⠀⠀⠀⠀⠀⠀⠀⢀⡧⠔⠋⠀⠀⠀⠀⠀⠈⡆⢀⠎⠀⠀⠀⠀⠀⠀⠱⣇⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⢀⠇⠀⠀⠀⣀⣀⠤⠤⠒⠁⡇⠀⠀⠀⠀⠀⠀⠀⠀⢱⠎⠀⠀⠀⠀⠀⠀⠀⢰⠁⠑⢤⡀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⢸⠀⡠⠊⠉⠀⠀⠀⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⢀⠴⠉⢆⠀⠀⠀⠀⠀⠀⢠⠇⠀⠀⠀⠈⠒⠤⣀⠀⠱⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠘⡔⠁⠀⠀⠀⠀⠀⠀⠀⠀⢱⠀⠀⠀⠀⢀⠔⠉⠀⠀⠀⢣⠀⠀⠀⠀⢠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠉⠑⠚⠲⠤⠤⠄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⡄⠀⢠⠎⠁⠀⠀⠀⠀⠀⠀⠳⡀⠀⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠈⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠱⡀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠢⡄⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠙⢆⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀ ⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠘⠆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

```forth
: spiral-rec 1 + dup move 92 turn dup 110 < if spiral-rec then ;
: spiral start 1 spiral-rec show ;

spiral
```

Using a similar trick to the the `rose`; incrementing the `move` distance with `1 + dup`.

Another interesting thing is to notice that `spiral-rec` doesn't use a `loop`. Instead it recursively calls itself! Recursion and higher order functions? Forth is quite a little functional language!

```text
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⠎⠢⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣄⣀⢠⠎⠀⠀⠀⠉⠢⢄⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡜⠀⢠⠓⠒⠤⣀⡀⠀⠀⠀⠈⠒⠤⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡖⠢⡴⣁⣰⠃⠀⠀⠀⠀⠈⠓⠢⢄⣀⠀⠀⠈⠒⠤⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢰⠁⢰⠁⡰⠁⠉⠉⠒⠢⠤⣀⡀⠀⠀⠀⠉⠲⠤⣀⠀⠈⠑⠢⣄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡗⠒⡞⢒⠧⡼⠤⢄⣀⣀⣀⠀⠀⠀⠈⠉⠑⠒⠢⠤⣀⣀⠉⡑⠢⢄⡠⢗⢢⣤⠤⢆⠀⣀⣀⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⢰⢁⣎⠞⠀⠀⠀⠀⠀⠀⠉⠉⠉⠉⠒⠒⠒⠢⡴⢤⣔⣋⣭⣺⠥⢼⣋⣷⢤⣟⣻⣍⡠⠤⢼⠤⠒⠒⢲⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢰⠒⢺⠒⡗⣞⠞⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⠒⢒⣶⣲⣚⣶⣷⠿⢗⠛⠚⠛⡟⠛⠛⢻⠓⠛⠳⡾⣷⣶⣗⣒⣒⣺⠒⠒⠒⢲⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢸⠀⡜⢸⣸⣋⣀⡠⠤⠤⠤⠒⠒⠒⠊⣉⣩⢿⣯⣾⠞⠋⠑⢆⡀⠀⠑⢤⠀⠈⢣⡀⠀⠣⡀⠀⢱⠀⠈⢹⠓⠶⣼⡭⣉⡉⢺⠒⠒⠢⢤⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡖⢺⠉⡏⣯⠃⠀⠀⠀⢀⣀⠤⠔⠒⠊⣩⢴⣻⡟⠉⠈⠑⠢⣄⡀⠈⠣⢄⡀⠙⢄⡀⠘⣄⠀⠱⡀⠀⢣⠀⠀⡇⠀⠀⡟⠲⣪⢽⣑⠒⠢⡮⣀⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢇⢸⣀⣿⠧⠤⠒⠊⠉⠁⣀⠤⠔⢊⣩⣾⡟⠁⠈⠉⠒⠢⠤⣀⡈⠑⠢⣄⠑⠢⣄⠑⢆⡀⠣⡀⠑⡄⠈⣆⠀⢸⠀⠀⢇⠀⠀⢹⠢⢍⢢⠧⣀⠈⢉⠇⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠐⡖⢺⠉⣿⡏⠀⢀⡠⠤⠒⠉⣁⠤⢊⡱⣿⠋⠈⠉⠉⠒⠒⠢⠤⠤⣈⣑⠒⠤⢍⣒⠤⣑⠢⡘⢤⠘⢄⠘⣄⠈⡆⠈⡆⠀⢸⠀⠀⡇⠀⠀⢹⠒⢤⠉⡝⠦⢄⡀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢱⢈⣞⣿⠕⠊⠁⠀⣀⠤⠋⣀⠔⡡⣿⠏⠉⠉⠉⠉⠒⠒⠒⢒⣒⣒⣒⣭⣭⣵⡶⢿⠵⢿⢾⣵⣍⣦⣣⡌⢆⠸⡀⢸⠀⢸⠀⠀⡇⠀⠀⡎⠀⠀⢩⠓⢄⡀⠈⡏⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⢒⢏⣟⡏⢀⡠⠔⠊⠀⡠⠊⡡⢎⢼⡟⠒⠉⠉⠉⣉⣉⠭⠭⣕⡲⣶⡿⠫⡍⠱⡀⠘⡄⠈⡆⠀⡏⠙⢻⢾⣾⣦⢱⠀⡇⠈⡆⠀⡇⠀⢠⠇⠀⠀⡎⠀⠀⠈⡝⠦⣀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⢯⢸⣹⡗⠉⠀⢀⠤⠋⢀⠖⣡⢊⣿⡗⠒⠒⠉⣉⡠⠤⣒⠭⣲⣽⣋⠑⢄⠘⣄⠘⡄⢱⠀⢇⠀⡇⠀⡎⠀⢹⠻⣯⣷⢸⠀⡇⠀⡇⠀⡸⠀⠀⡸⠀⠀⠀⡜⠀⠀⠀⢑⠆⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⠣⣲⢫⣧⡇⣠⠒⠁⢀⡔⠁⡰⢁⢎⢿⡧⠒⠊⢉⡠⠔⣩⠔⡭⣿⠣⣄⠓⢌⠱⣈⢢⠱⡀⢇⢸⠀⡇⠀⡇⠀⡎⠀⢨⠻⣯⣷⢷⠀⡇⠀⡇⠀⢠⠃⠀⠀⡜⠀⠀⠀⣀⠎⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡰⠁⢀⠞⢼⣻⣯⠀⠀⡔⠃⢀⠎⢁⠇⡜⡎⣷⠔⠊⢁⠴⢊⠴⢩⡻⡻⡒⠢⢍⣒⢍⡦⡣⡵⡹⡸⡀⡇⢇⢀⠇⢰⠁⢠⠋⠀⢹⣿⣿⠤⡇⢰⠁⠀⡎⠀⠀⡼⠀⠀⠀⡰⠁⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⡰⠁⠀⡎⠐⣏⢻⣿⡠⠋⠀⢠⠊⢀⠎⢰⢁⢷⢹⣧⡚⢁⡔⢉⠜⡱⢱⢹⡬⠭⠵⡒⢵⠚⡾⢯⢷⣷⣣⢣⢸⢸⠀⡎⠀⡎⠀⡰⠁⠘⣿⡻⣇⡸⠀⡸⠀⠀⡜⠀⠀⢀⠜⠁⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⡜⠀⠀⡸⠀⢸⠀⡧⣿⣧⠀⡴⠁⢀⠎⠀⡇⢸⢸⢸⢹⢿⣧⣠⠃⡜⢠⠃⡇⣇⣈⡇⡇⣎⠞⡰⢁⠼⠉⣿⣾⢸⢸⢰⠁⡜⠀⡴⠁⢀⠜⢹⣿⢎⡗⢀⠇⠀⡸⠀⠀⢠⠊⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⢀⠞⠀⠀⡰⠁⠀⡎⠀⡗⢼⣻⣿⠀⢀⠎⠀⡜⠀⡇⢸⠈⡎⡞⡵⡫⡻⡷⣳⢞⡭⢕⣒⠚⡝⡜⡜⡱⠊⡤⠚⢀⠽⣿⡇⡎⡸⠁⡜⠀⡠⠋⠀⢈⣿⢿⠱⡜⠀⡜⠀⠀⡴⠁⠀⠀⠀⠀⠀⠀⠀⠀
⡀⠀⠀⠀⠀⢀⠎⠀⠀⢠⠃⠀⢰⠁⢠⠃⢸⢿⣻⣧⡞⠀⢰⠁⢠⠃⢸⠀⡇⢱⠸⡘⡌⢮⠢⡕⠭⡑⠢⢍⣽⣪⢊⠔⢉⠔⠊⢁⡠⢿⣷⢱⢁⠞⢀⠜⠁⢠⠔⠁⢹⣾⣻⠋⡰⠁⢀⠜⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠈⠑⢄⡀⢠⠋⠀⠀⢀⠇⠀⠀⡎⠀⢸⠀⢸⠀⡏⣯⢷⣄⡇⠀⢸⠀⢸⠀⢇⠈⡆⠣⡘⢆⠱⡌⠣⣈⠒⢤⡿⣜⠕⣊⠥⠔⠊⣁⣀⢼⣏⢇⠎⡰⠉⡠⠜⠁⢀⡠⢼⣏⡇⡿⡀⣠⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⡨⠳⢄⠀⢀⡜⠀⠀⢰⠁⠀⡸⠀⢸⠀⢸⠈⡏⣾⣷⣦⣇⠀⢸⠀⢸⠀⢱⠀⢣⠈⢆⠈⠦⣈⣶⡿⢭⣊⡩⠴⠒⠊⠉⣀⣀⣸⣯⢃⠎⣀⠖⠁⡠⠔⠊⠀⢸⣼⡼⡗⡱⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠐⠥⢄⡀⠙⡾⢄⠀⠀⡇⠀⠀⡇⠀⢸⠀⠘⡄⢸⠀⢏⠹⡿⡷⣾⣤⣸⣀⠈⡆⠈⣆⣈⣧⡶⠿⠯⠭⠓⠒⠒⠊⠉⠉⠉⠀⠀⣾⡳⢡⠚⢀⡔⠊⠁⢀⡠⠖⣽⡿⣇⠿⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠈⡲⠥⣈⠑⢺⡀⠀⠀⡇⠀⢸⠀⠀⡇⠀⢇⠈⢆⠑⡌⠪⡙⢽⠻⣟⠿⣛⡽⠿⣛⡛⠫⠭⠭⠭⣉⣉⣉⡉⠉⠉⠉⣹⡯⢏⡡⠊⢁⡠⠴⠊⠁⠀⢀⣿⡧⢾⠒⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠠⠧⣀⡀⠙⡗⠬⣱⢴⡁⠀⠈⡆⠀⢸⠀⠘⡄⠈⢇⠈⢦⠉⢆⡉⠦⡉⠲⢌⠑⠢⢌⣉⠑⠒⠤⠤⣀⣀⠈⠉⢉⡽⡿⠗⣁⠴⠊⠁⣀⡠⠤⠒⠊⣽⡿⢹⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠈⢹⠒⠢⢄⣹⠪⢧⣄⡇⠀⠘⡄⠀⠸⡀⠈⢆⠀⠣⡀⠑⢄⠈⠒⣄⠉⠲⢄⡀⠉⠒⠤⣀⠀⠈⠉⣲⣾⠿⠗⣉⡠⠤⠒⠊⠀⠀⠀⣀⣰⡯⡧⢼⠒⠓⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠚⠢⠤⠤⣸⣉⡑⠪⡷⣢⣄⣇⠀⠀⢇⠀⠘⢆⠀⠘⣄⠀⠓⡄⠀⠑⢆⡀⠈⠑⠤⣀⠀⢉⣵⣾⢯⠞⢊⣉⣀⠤⠤⠔⠒⠊⠉⠉⣠⢇⠇⡇⠈⡆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣇⣀⣈⣉⡏⠒⠚⢽⠶⣤⣜⣄⠀⠈⢆⠀⠈⢆⠀⠈⠣⡄⠀⠘⠢⣄⣤⣴⡿⡿⣻⠚⠊⣉⣁⣀⣀⣀⣀⣀⠤⠤⠤⡼⡽⢼⠤⡗⠒⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣏⣉⣉⣉⣏⣉⣙⣽⡿⠿⣾⢶⣶⣶⢷⣶⣶⠾⣶⡿⡿⢿⣽⣏⣟⣉⣉⣉⣉⠀⠀⠀⠀⠀⠀⠀⠀⢀⣮⢣⠃⡎⠀⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢇⠤⠤⠖⢟⠪⣍⡹⡦⢞⣉⠷⢶⣍⡝⠙⠖⠚⠤⠤⢄⣀⡀⠀⠀⠉⠉⠉⠉⠉⠉⠉⡲⢳⢓⠗⢲⠓⠒⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠉⠀⠉⠺⣉⠈⠓⠪⢄⡈⠉⠒⠢⠤⣀⡀⠀⠈⠉⠉⠒⠒⠢⠤⢄⣀⣴⢡⠃⡸⠀⡸⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⠦⡀⠀⠈⠒⠤⣀⠀⠀⠈⠉⠒⠤⢄⣀⠀⠀⠀⢀⠎⢰⠉⢩⠓⠒⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠓⠤⡀⠀⠀⠉⠒⠤⣀⠀⠀⠀⠀⠉⠑⡲⠥⣤⡃⠀⡎⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠒⣄⠀⠀⠀⠀⠑⠢⢄⡀⠀⡜⠁⢰⠃⠈⠙⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠢⣀⠀⠀⠀⠀⢨⠛⠢⢤⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⠤⣀⡰⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
```

Have fun!

## Next

Next let's move on to building a more native Forth for a [register machine](../../hardware/register/).